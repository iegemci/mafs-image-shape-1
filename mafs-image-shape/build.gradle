apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.mafstech.libs'
version = '1.0.4' //LIBRARY VERSION

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.0'
    testCompile 'junit:junit:4.12'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'com.mafstech.libs' // CREATE A GROUP ID FOR YOUR LIBRARY
                artifactId 'mafs-image-shape' // THE NAME OF YOUR MODULE

                name 'Mafs Image Shape' // YOUR LIBRARY NAME
                description 'A collections of methods to shape image.' // YOUR LIBRARY DESCRIPTION
                url 'https://mafstech.com' // YOUR SITE

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mafujuls' //YOUR ID
                        name 'Mafujul Islam' //YOUR NAME
                        email 'mafujuls@gmail.com' //YOUR EMAIL
                    }
                }
                scm {
                    connection 'https://github.com/mafujuls/mafs-image-shape' // YOUR GIT REPO
                    developerConnection 'https://github.com/mafujuls/mafs-image-shape.git' // YOUR GIT REPO
                    url 'https://libs.mafstech.com' // YOUR SITE

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'mafs-image-shape' //YOUR PACKAGE NAME
        desc = 'A collections of methods to shape image.' // YOUR LIBRARY DESCRIPTION
        websiteUrl = 'libs.mafstech.com' // YOUR SITE
        vcsUrl = 'https://github.com/mafujuls/mafs-image-shape.git' // YOUR GIT REPO
        licenses = ["Apache-2.0"] // A LIST OF YOUR LICENCES
        publish = true
        publicDownloadNumbers = true
    }
}